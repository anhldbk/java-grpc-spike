buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.0'
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'
apply plugin: 'application'

description = "grpc spike"
startScripts.enabled = false
run.enabled = false

repositories {
  mavenCentral()
  jcenter()
  mavenLocal()
  maven { url "http://oss.sonatype.org/content/repositories/snapshots" }
}

sourceSets {
  main {
    java {
      srcDir 'src/main/gen'
    }
    proto {
      srcDir 'src/main/proto'
    }
  }
}

protobuf {
  generatedFilesBaseDir = "$projectDir/src"
  protoc {
    artifact = "com.google.protobuf:protoc:3.0.0-beta-1"
  }
  //noinspection GroovyAssignabilityCheck
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:0.8.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
        outputSubDir = 'gen'
      }
    }

    all().each { task ->
      task.builtins {
        java {
          outputSubDir = 'gen'
        }
      }
    }

  }
}

dependencies {
  compile 'ch.qos.logback:logback-classic:1.1.3'
  compile 'com.google.inject:guice:4.0'
  compile 'com.google.protobuf:protobuf-java:3.0.0-beta-1'
  compile 'io.grpc:grpc-all:0.8.0'
  compile 'org.slf4j:slf4j-api:1.7.12'
}

task helloWorldServer(type: CreateStartScripts) {
  mainClassName = "net.dehora.grpc.spike.service.GrpcServerMain"
  outputDir = new File(project.buildDir, 'tmp')
  applicationName = "hello-world-server"
  classpath = jar.outputs.files + project.configurations.runtime
}

task helloWorldClient(type: CreateStartScripts) {
  mainClassName = "net.dehora.grpc.spike.service.GrpcGreeterClient"
  outputDir = new File(project.buildDir, 'tmp')
  applicationName = "hello-world-client"
  classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
  from(helloWorldServer)
  from(helloWorldClient)
  fileMode = 0755
}